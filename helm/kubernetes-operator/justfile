set quiet := false

name := "netbird-operator"

image := "local/netbirdio-operator:dev"
docker_context := "../.."

helm_path := "."

k3d_config := "k3d.yaml"
kubeconfig := "~/.kube/config"

# List all commands
list:
    just --list --unsorted

########################################################
# Docker

# Build image
build-image:
    docker build -t {{ image }} -f {{ docker_context }}/Dockerfile {{ docker_context }}

########################################################
# K3d

# Create k3d cluster
create-cluster:
    k3d cluster create {{ name }} --config {{ k3d_config }}

# Delete k3d cluster
delete-cluster:
    k3d cluster delete {{ name }}

# Start k3d cluster
start-cluster:
    k3d cluster start {{ name }}

# Stop k3d cluster
stop-cluster:
    k3d cluster stop {{ name }}

# Import image into k3d cluster
import-image:
    k3d image import {{ image }} --cluster {{ name }}

# Build image and import into k3d cluster
build-and-import:
    @echo "Building image..."
    just build-image
    @echo
    @echo "Importing image into k3d cluster..."
    just import-image

########################################################
# kubectl

_kubectl *args:
    kubectl {{ args }} \
        --namespace {{ name }} \
        --kubeconfig {{ kubeconfig }} \
        --context k3d-{{ name }}

########################################################
# Helm

_helm *args:
    helm {{ args }} \
        --namespace {{ name }} \
        --kubeconfig {{ kubeconfig }} \
        --kube-context k3d-{{ name }}

# Update helm dependencies
update-deps:
    @echo "Updating helm dependencies..."
    helm dependency update --skip-refresh {{ helm_path }}

# Install cert-manager CRDs
install-cert-manager-crds:
    @echo "Installing cert-manager CRDs..."
    kubectl apply \
        -f https://github.com/cert-manager/cert-manager/releases/download/v1.17.0/cert-manager.yaml \
        --kubeconfig {{ kubeconfig }} \
        --context k3d-{{ name }}

# Install chart
install:
    @echo "Installing cert-manager CRDs..."
    just install-cert-manager-crds
    @echo
    @echo "Updating helm dependencies..."
    just update-deps
    @echo
    @echo "Installing {{ name }} chart..."
    just _helm upgrade \
        --install {{ name }} {{ helm_path }} \
        --create-namespace \
        --values {{ helm_path }}/values.yaml \
        --values {{ helm_path }}/values.dev.yaml \
        --values {{ helm_path }}/secrets.yaml

# Uninstall chart
uninstall:
    @echo "Uninstalling {{ name }} chart..."
    just _helm uninstall {{ name }} 2> /dev/null || true
    @echo "Deleting namespace {{ name }}..."
    just _kubectl delete namespace {{ name }} 2> /dev/null || true
    @echo "Done"
